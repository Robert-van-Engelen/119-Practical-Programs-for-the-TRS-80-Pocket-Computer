' p.257 Spherical Triangles
10 "SSS" GOSUB 130 : J=-1 : GOSUB 20 : GOTO 160
20 D=ACS((COS A+J*COS B*COS C)/SIN B/SIN C)
30 E=ACS((COS B+J*COS A*COS C)/SIN A/SIN C)
40 F=ACS((COS C+J*COS A*COS B)/SIN A/SIN B) : RETURN
50 "AAA" GOSUB 130
60 J=1 : GOSUB 20 : FOR Z=1 TO 3 : T=A(Z) : A(Z)=A(Z+3) : A(Z+3)=T : NEXT Z : GOTO 160
70 "SAS" GOSUB 130 : B=ACS(COS B*SIN A*SIN C+COS A*COS C) : J=-1 : GOSUB 20 : GOTO 160
80 "ASA" GOSUB 130 : B=ACS(COS B*SIN A*SIN C-COS A*COS C) : GOTO 60
90 "SSA" GOSUB 130 : D=C : E=ASN(SIN B*SIN D/SIN A)
100 GOSUB 140 : G=(A<B)*I : GOTO 160
110 "AAS" GOSUB 130 : D=A : E=B : A=C : B=ASN(SIN E*SIN A/SIN D)
120 GOSUB 140 : H=(D<E)*I : GOTO 160
130 INPUT A,B,C : I=1 : G=0 : H=0 : RETURN
140 C=2*ATN(SIN((D+E)/2)*TAN((A-B)/2)/SIN((D-E)/2))
150 F=ACS((COS C-COS A*COS B)/SIN A/SIN B : RETURN
160 PRINT "SIDE, OPP ANGLE" : PRINT A,D : PRINT B,E : PRINT C,F
170 IF G BEEP 1 : E=ACS -COS E : I=0 : GOTO 100
180 IF H BEEP 1 : B=ACS -COS B : I=0 : GOTO 120
190 GOTO 1
